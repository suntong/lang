
/* Lexical part */
!whitespace : ' ' | '\t' | '\n' | '\r' ;
!comment : _lineComment | _blockComment ;
_lineComment : '/' '/' {.} '\n' ;
_blockComment : '/' '*' {. | '*'} '*' '/' ;

_letter : 'A' - 'Z' | 'a' - 'z' | '_' ;
_digit : '0' - '9' ;

_byte_value       : _octal_byte_value | _hex_byte_value ;
_octal_byte_value : '\\' _octal_digit _octal_digit _octal_digit ;
_hex_byte_value   : '\\' 'x' _hex_digit _hex_digit ;
_little_u_value   : '\\' 'u' _hex_digit _hex_digit _hex_digit _hex_digit ;
_big_u_value      : '\\' 'U' _hex_digit _hex_digit _hex_digit _hex_digit
                            _hex_digit _hex_digit _hex_digit _hex_digit ;

_octal_digit : '0' - '7' ;
_hex_digit
  : '0' - '9'
  | 'A' - 'F'
  | 'a' - 'f'
  ;


int64 : '1'-'9' {_digit} ;

//id : _letter {_letter | _digit} ;


/* Syntax part */

<< 
import (
	"github.com/suntong/lang/lang/Go/src/parsers/gocc/psr1-calc/token"
	"github.com/suntong/lang/lang/Go/src/parsers/gocc/psr1-calc/util"
)
>>

Calc
  : Expr
  | Expr ";" Expr	<< $2, nil >>
  ;

Expr
  : Expr "+" Term	<< $0.(int64) + $2.(int64), nil >>
  | Expr "-" Term	<< $0.(int64) - $2.(int64), nil >>
  | Term			
  ;

Term
  : Term "*" Factor	<< $0.(int64) * $2.(int64), nil >>
  | Term "/" Factor	<< $0.(int64) / $2.(int64), nil >>
  | Factor			
  ;

Factor
  : "(" Expr ")"	<< $1, nil >>
  | int64    		<< util.IntValue($0.(*token.Token).Lit) >>
;

