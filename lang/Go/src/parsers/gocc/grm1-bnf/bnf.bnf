/*
 Copyright (c) 2021 Tong Sun
*/

/* Lexical part */
!whitespace : ' ' | '\t' | '\v' | '\f' | '\r' | '\n';
!lineComment  : '/' '/' { . } '\n' ;
!blockComment : '/' '*' { . | '*' } '*' '/' ;

_letter : 'A'-'Z' | 'a'-'z' | '_' ;
_digit : '0'-'9' ;
_alpha : _letter | _digit ;
_any : ' '  |  '!'  | '#' - '~' ;

string_literal : '"' {_any}  '"' ;
//int : '0' | '1'-'9' {_digit} ;

ident
    : _letter { _alpha }
    ;

/* punctuation */
plus : '+' ;
// minus : '-' ;
mul : '*' ;
// div : '/' ;

bar : '|' ;
lbrace : '{' ;
rbrace : '}' ;
lparen : '(' ;
rparen : ')' ;

lt : '<' ;
gt : '>' ;

lend : '[' ;
rend : ']' ;

//assign : '=' ;
assign : ':'  ':'  '=' ;
semicolon : ';' ;

/*
eq : '=' '=' ;
neq : '!' '=' ;
comma : ',' ;
*/

/* Syntax part */

<< 
import (
	// "github.com/suntong/gocc-grammars/bnf/token"
	// "github.com/suntong/gocc-grammars/bnf/util"
)
>>


RuleList
    : Rule
    | RuleList Rule
    ;

Rule
    : Lhs assign Rhs semicolon
    ;

Lhs
    : Id_
    ;

Rhs
    : Alternatives
    ;

Alternatives
    : Alternative
    | Alternatives bar Alternative
    ;

Alternative
    : Element
    | Alternative Element
    ;

Element
    : Optional_
    | Zeroormore
    | Oneormore
    | Text_
    | Id_
    | Element mul
    | Element plus
    ;

Optional_
    : lend Alternatives rend
    ;

Zeroormore
    : lbrace Alternatives rbrace
    ;

Oneormore
    : lparen Alternatives rparen
    ;


Text_
    : ident
    | string_literal
    ;


Id_
    : Ruleid
    | lt Ruleid gt
    ;

Ruleid
    : ident
    ;
