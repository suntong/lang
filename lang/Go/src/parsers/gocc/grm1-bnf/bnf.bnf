/*

Extracted from https://www.bottlecaps.de/rr/ui on Tue Jan 11, 2022, 22:48 (UTC-05)
Change it from LR(1) to LR(0) grammar by adding the ending ";" symbol

Copyright (c) 2021 Tong Sun

*/

/* Lexical part */
!whitespace : ' ' | '\t' | '\n' | '\r' ;
!comment : _lineComment | _blockComment ;
_lineComment : '/' '/' {.} '\n' ;
_blockComment : '/' '*' {. | '*'} '*' '/' ;

id : _letter {_letter | _digit} ;

_letter : 'A' - 'Z' | 'a' - 'z' | '_' ;
_digit : '0' - '9' ;

char_lit
  : '"' (_unicode_value | _byte_value) '"'
  | '\'' (_unicode_value | _byte_value) '\''
  ;

string_lit
  : '"' {_unicode_char} '"'
  | '\''{_unicode_char} '\''
  ;

_unicode_value    : _unicode_char | _little_u_value | _big_u_value | _escaped_char ;
_unicode_char
  : .                   // Any UTF-8 character literal
  ;
_byte_value       : _octal_byte_value | _hex_byte_value ;
_octal_byte_value : '\\' _octal_digit _octal_digit _octal_digit ;
_hex_byte_value   : '\\' 'x' _hex_digit _hex_digit ;
_little_u_value   : '\\' 'u' _hex_digit _hex_digit _hex_digit _hex_digit ;
_big_u_value      : '\\' 'U' _hex_digit _hex_digit _hex_digit _hex_digit
                            _hex_digit _hex_digit _hex_digit _hex_digit ;

_octal_digit : '0' - '7' ;
_hex_digit
  : '0' - '9'
  | 'A' - 'F'
  | 'a' - 'f'
  ;

_escaped_char : '\\' ( 'a' | 'b' | 'f' | 'n' | 'r' | 't' | 'v' | '\\' | '\'' | '"' ) ;
repeat_char : ( '?' | '*' | '+' ) ;

url_lit
  : '[' {_unicode_char} ']' ;



/* Syntax part */

Grammar
  : Production
  | Grammar Production
  ;

Production
  : NCName "::=" LexPattern ";"
  ;

NCName
// [http://www.w3.org/TR/xml-names/#NT-NCName]
  : id
  ;

LexPattern 
  : LexAlt
  | LexAlt "|" LexPattern
  ;

LexAlt 
  : LexTerm
  | LexAlt LexTerm
  ;

LexTerm
  : "."
  | id
  | char_lit
  | char_lit "-" char_lit
  | string_lit
  | url_lit
  | LexPattern repeat_char
  | "[" LexPattern "]"
  | "{" LexPattern "}"
  | "(" LexPattern ")"
  | empty
  ;

